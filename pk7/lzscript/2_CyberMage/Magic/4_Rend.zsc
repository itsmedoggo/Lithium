// Distributed under the CC0 public domain license.
// By Alison Sanderson. Attribution is encouraged, though not required.
// See licenses/cc0.txt for more information.

class Lith_RendHitSound : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 {
         A_PlaySound("weapons/rend/hit", CHAN_5);
         CallACS("Lith_Rend", true);
      }
      stop;
   }
}

class Lith_RendHit : FastProjectile
{
   default
   {
      DamageFunction 50 * random(1, 2);
      Height 8;
      Radius 8;
      Speed 196;

      +FORCEPAIN
   }

   states
   {
   Spawn:
      TNT1 A 1;
      stop;
   XDeath:
      TNT1 A 0 A_GiveInventory("Lith_RendHitSound", 1, AAPTR_TARGET);
      stop;
   }
}

class Lith_RendHit2 : Lith_RendHit {default {DamageFunction 50 * random(2, 3);}}

class Lith_Rend : Lith_CWeapon;

default
{
   Tag "Romyetya";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/rend/draw";

   +WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 ACS_ScriptCall("Lith_MagicSelect", 4);
SelectLoop:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 0 A_JumpIf(CountInv("Lith_MagicAmmo") < 10, "ReadySelector");
   TNT1 A 5 {
      A_TakeInventory("Lith_MagicAmmo", 10);
      CallACS("Lith_Rend", false, random(1, 4));
      A_PlaySound("weapons/rend/slash", CHAN_7);
      A_FireProjectile("Lith_BladeLightProjectile", 0, 0, 0, 0, 0, 0);
      A_AlertMonsters();
      if(CountInv("PowerStrength"))
         A_FireProjectile("Lith_RendHit2");
      else
         A_FireProjectile("Lith_RendHit");
   }
   TNT1 A 0 A_ReFire;
   TNT1 A 5 A_PlaySound("weapons/rend/end", CHAN_6);
   goto ReadySelector;
}

// EOF
